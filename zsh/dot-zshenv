#!/usr/bin/env zsh
setopt no_beep
setopt no_global_rcs
setopt pushd_silent

# Filesystem Hierarchy
## Load XDG Definitions (Single Source of Truth)
if [[ -f "$HOME/.config/shell/xdg.env.sh" ]]; then
	source "$HOME/.config/shell/xdg.env.sh"
fi
## Always use Local Workspaces
export PREFIX="${HOME}/.local"
export TMPDIR="${HOME}/.local/tmp"
mkdir -p "${TMPDIR}"

export DEBUG_LOG_DIR="${TMPDIR}/logs_$(date -I)"
mkdir -p "${DEBUG_LOG_DIR}"

# PATHs
## initialize PATH
typeset -gx PATH='/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin'
## Homebrew PATHs with `eval $(brew shellenv)`
typeset _brew_prefix=""
typeset _uname_system="$(uname -s)"
typeset _uname_machine="$(uname -m)"
if [[ "$_uname_system" == "Linux" ]]; then
	_brew_prefix="/home/linuxbrew/.linuxbrew"
elif [[ "$_uname_system" == "Darwin" ]]; then
	if [[ "$_uname_machine" == "arm64" ]]; then
		_brew_prefix="/opt/homebrew"
	else
		_brew_prefix="/usr/local"
	fi
fi
if [[ -n "$_brew_prefix" && -x "${_brew_prefix}/bin/brew" ]]; then
	eval "$("${_brew_prefix}/bin/brew" shellenv)"
	export FPATH="${HOMEBREW_PREFIX}/share/zsh/site-functions:${FPATH}"
fi
unset _brew_prefix _uname_machine _uname_system
## use GNU on BSD
if [[ "Darwin" = "$(uname -s)" && -n "${HOMEBREW_PREFIX:-}" ]]; then
	## gnubin
	PATH="${HOMEBREW_PREFIX}/opt/coreutils/libexec/gnubin:$PATH"
	PATH="${HOMEBREW_PREFIX}/opt/findutils/libexec/gnubin:$PATH"
	PATH="${HOMEBREW_PREFIX}/opt/gnu-sed/libexec/gnubin:$PATH"
	PATH="${HOMEBREW_PREFIX}/opt/gettext/bin:$PATH"
fi
if [[ "Darwin" = "$(uname -s)" ]]; then
	## I use podman on Mac
	PATH="/opt/podman/bin/:${PATH}"
fi
## under current directory
PATH="./node_modules/.bin:${PATH}"
PATH="./vendor/bin:${PATH}"
export PATH="${PATH}"

# Editor, Terminal
export EDITOR="vim"
export TERMINFO="${HOME}/.terminfo"

# Security
export DOCKER_CONTENT_TRUST=1

# Languages
# - Never use LC_ALL (debugging only).
# - Set specific LC_* only if necessary.
# (Experts may disregard these rules.)
export LANG="en_US.UTF-8"

# ENVs for Runtimes
export ENV="development"
export NODE_ENV="development"
