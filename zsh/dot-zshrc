#!/usr/bin/env zsh
# FAILSAFE
if [[ -f "$HOME/.config/shell/xdg.env.sh" ]]; then
	source "$HOME/.config/shell/xdg.env.sh"
fi

# zsh native
## Disable some builtins
disable r
## History Settings
HISTSIZE=1000000000
SAVEHIST=1000000000
HISTFILE="${XDG_STATE_HOME:-$HOME/.local/state}/zsh_history"
[[ -d "$(dirname "$HISTFILE")" ]] || mkdir -p "$(dirname "$HISTFILE")"
## cd hook and directory list manager
autoload -Uz add-zsh-hook chpwd_recent_dirs
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':chpwd:*' recent-dirs-max 0

# My Plugin Manager
_ZPLUGIN_DIR="${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins"
mkdir -p ${_ZPLUGIN_DIR}
function zload() {
	local repo="$1"
	local plugin_name="${repo:t}"
	local plugin_dir="${_ZPLUGIN_DIR}/${plugin_name}"

	# download plugin package
	if [[ ! -d "$plugin_dir" ]]; then
		git clone --depth 1 --recursive "https://github.com/${repo}.git" "$plugin_dir"
	fi

	# you must add fpath before `compinit`
	fpath=("${plugin_dir}" $fpath)

	# if plugin has any constructor script, import it.
	if [[ -f "${plugin_dir}/${plugin_name}" ]]; then # e.g. "arzzen/calc.plugin.zsh"
		source "${plugin_dir}/${plugin_name}"
	elif [[ -f "$plugin_dir/${plugin_name}.plugin.zsh" ]]; then # e.g. "chrissicool/zsh-256color"
		source "$plugin_dir/${plugin_name}.plugin.zsh"
	elif [[ -f "$plugin_dir/${plugin_name}.zsh" ]]; then # legacy package
		source "$plugin_dir/${plugin_name}.zsh"
	fi
}
function zupdate() {
	find "$_ZPLUGIN_DIR" -mindepth 1 -maxdepth 1 -type d | while read -r dir; do
		[[ -d "$dir/.git" ]] || continue
		git -C "$dir" pull
	done
}

# Completions
## My Local Completions
fpath=("${XDG_DATA_HOME:-$HOME/.local/share}/zsh/site-functions" $fpath)
## Execute compinit
autoload -Uz compinit
if [[ -n ${HOME}/.zcompdump(#qN.mh+24) ]]; then
	compinit -C
else
	compinit
	zcompile ${HOME}/.zcompdump
fi
autoload -Uz bashcompinit && bashcompinit

# Prompt Theme
autoload -Uz promptinit && promptinit
prompt ndxbn

# Local Plugins
for config in "${_ZPLUGIN_DIR}/_local/"*.zsh(N); do
	[[ -f "$config" ]] && source "$config"
done
for config in "${XDG_CONFIG_HOME}/zsh/"*.zsh(N); do
	[[ -f "$config" ]] && source "$config"
done

# External Plugins
zload "zsh-users/zsh-autosuggestions"
zload "chrissicool/zsh-256color"

if [[ -f ${HOME}/.local.zshrc ]]; then
	source ${HOME}/.local.zshrc
fi

## The official zsh-syntax-highlighting documentation requires sourcing it at the end of ".zshrc"
##   because it installs ZLE hooks and wraps ZLE widgets.
zload "zsh-users/zsh-syntax-highlighting"
